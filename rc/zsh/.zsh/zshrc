# Disable the global Zsh configuration file
unset global_rcs

# keep some arrays unique
typeset -U path
typeset -U fpath
typeset -U cdpath
typeset -U manpath

# some variables to export
export OSNAME=`uname -s`

# add our function path
functions_directory=~/.zsh/functions
if [[ -d $functions_directory ]]; then
    fpath=($functions_directory $fpath)
fi

# history stuff
HISTSIZE=1000
SAVEHIST=1000
if [[ -d /home/$USER ]] ; then
    HISTFILE=/home/$USER/.history
fi
setopt append_history 
setopt inc_append_history 
setopt extended_history 
setopt hist_find_no_dups
setopt hist_reduce_blanks 
setopt hist_ignore_space
setopt hist_no_store 
setopt hist_no_functions 
setopt no_hist_beep

# clober stuff
setopt no_clobber 
setopt hist_allow_clobber

# other options
setopt no_beep                  # Turn it off, puh-lease
setopt auto_cd                  # Cd into directory if name is specified
setopt auto_list                # List options on ambiguous completion
setopt multios
setopt no_correct               # Do not correct spelling
setopt no_correct_all           # Do not try to correct spelling of all arguments
setopt interactive_comments     # Allow comments on command line
setopt no_hup                   # Do not send HUP signal to jobs when exiting

# add some path/man directories
path=(~/bin ~/local/bin $path)
manpath=(/usr/man /usr/share/man /usr/local/man /usr/local/share/man ~/local/man $manpath)

git_prompt_status() {
    la="\u276C"
    ra="\u276D"
    branch=`git rev-parse --abbrev-ref HEAD 2> /dev/null`
    test -n "$branch" && {
        remoterepo=`git remote -v | head -1 | awk '{print $2}'`
        repo=`basename "$remoterepo"`
        st=''
        test `git status --porcelain | wc -l` -ne 0 && {
            st="$st*"
        }
        print "%B%F{red}$repo %f$la%F{magenta}$branch%f$ra%F{red}$st%b"
    }
}

# prompt magic
autoload -U colors; colors
setopt prompt_percent
setopt prompt_subst
prompt_newline=$'\n%{\r%}'
prompt_backtick='\`'
PS1=$'%F{yellow}[%*] %B%F{cyan}%~%b \$(git_prompt_status)\n%B%F{yellow}%n%F{red}@%F{green}%m%b %F{cyan}%B\$%b%f '
PS2="%{$fg[yellow]%}%_%{$fg[red]%}>%{$reset_color%}"
# fix vim syntax `"

# autoload my functions
autoload -U ${fpath[1]}/*(:t)

# Misc
autoload zcalc
alias run-help >&/dev/null && unalias run-help
autoload run-help

# reverse unwanted aliasing of `which' by distribution startup
# files (e.g. /etc/profile.d/which*.sh); zsh's which is perfectly
# good as is.
alias which >&/dev/null && unalias which

# Make sure SHELL is defined for use by programs
if [[ "z$SHELL" = "z" ]] ; then
    export SHELL='zsh'
fi

# bring in other files
. ~/.zsh/aliases
. ~/.zsh/cvs
. ~/.zsh/hosts
. ~/.zsh/completion
. ~/.zsh/term
. ~/.zsh/dirs
. ~/.zsh/bindings

if [[ -e ~/.zshrc_local ]] ; then
    . ~/.zshrc_local
fi

# vim:tw=300
